<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: -> # called by Model ctor
        @refreshAgents = false # for static agents
        
        # globals
        @prob = 1
        
        # defaults
        @agents.setDefaultShape("circle")
        @agents.setDefaultColor([210,0,0])
        @agents.setDefaultSize(.4)
        @agents.setDefaultPen(2.5)
        @agents.setDefaultHeading(0) # override promotion to random angle

        @anim.setRate 10

        for p in @patches
          p.color = u.randomGray 160, 190
          p.occupied = false
        @drawGrid [0,0,0]
        @walker = @agents.create(1)[0]
        p0 = @patches[0]
        @walker.setXY p0.x, p0.y
        @walker.penDown = true
        # @walker.color = [210,0,0]
        # @walker.shape = "circle"
        # @walker.size = .4
        # @walker.setXY 0, 0 # needed? should start at 0,0
        @walker.stamp()
        @walker.p.occupied = true
        # @walker.penDown = true
        # @walker.penSize *= 2.5
        @setTextParams @patches, "9px sans-serif", "right", "bottom"
        @setLabelParams @patches, [255,255,255], [12,12]
      step: ->
        @floodFill()
        p = @asSet (n for n in @walker.p.n4 when n.ok)
        @drawNum p.length
        @walker.moveTo p.oneOf()
        @walker.stamp()
        @walker.p.occupied = true
        (@stop(); console.log "prob: #{@prob}") if @done()
      
      drawNum: (n) -> @walker.p.label = ""+n; @prob /= n
      done: -> @patches.last().occupied
      
      drawGrid: (c) ->
        a = @agents.create(1)[0]
        a.color = c 
        a.penSize *= .4
        a.heading = Math.PI/2
        for x in [0..@patches.maxX]
          a.setXY(x,0); a.penDown = true; a.forward @patches.maxY; a.penDown = false
        a.heading = 0
        for y in [0..@patches.maxY]
          a.setXY(0,y); a.penDown = true; a.forward @patches.maxX; a.penDown = false
        a.die()
      
      floodFill: ->
        p.ok = false for p in @patches
        pset = [@patches.last()] # [@patches.patchXY @patches.maxX, @patches.maxY] # [@patches.last()]
        while pset.length > 0
          p.ok = true for p in pset
          pnext = @asSet u.flatten (p.n4 for p in pset)
          pnext.sortById().uniq()
          pset = (n for n in pnext when not n.ok and not n.occupied)
        null

    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, hasNeighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new MyModel "layers", 30, 0,10, 0,10, false
    APP.setRootVars() # Put Model vars in global name space -- only for demo & debug

    </script>
  </head>
  <body onload="ABM.model.start()">
    <canvas id="test" style="height:0;width:0">Browser doesn't support HTML5 Canvas</canvas>
    <div id="layers" style="position:relative;padding:20;"></div>
  </body>
</html>