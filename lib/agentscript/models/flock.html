<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: -> # called by Model ctor
      
        @refreshPatches = false # <3fps->4fps agents larger part of drawing 
        # No optimizations: 4fps
        # @patches.usePixels() # 4fps same as refresh off
        @patches.cacheAgentsHere() # 17fps, big help for inRadius
        # w/ refresh off and two optimizations, 22-23fps
        @patches.cacheRect @vision, true # 22/23 -> 24; only +1+
        @agents.setUseSprites() # 24 -> 36
        
        @anim.setRate 60, false
        
        @population = 500     # agents
        @vision = 3.0         # patches
        @minSeparation = 0.75 # patches
        @maxTurn = 3.0        # degrees
        @maxTurn = u.degToRad(@maxTurn) # radians
      
        # defaults
        @agents.setDefaultSize(1.5)
        # @agents.setDefaultColor([255,0,0]) # slightly faster! ->38/39 ??

        p.color = u.randomGray 0, 100 for p in @patches
        @agents.create @population, (a) => # fat arrow for @patches
          #a.size = 1.5
          a.setXY @patches.randomPt()...
          a.color = u.randomMapColor() # u.randomColor() # u.randomBrightColor()

      # draw: -> # headless test .. max fps
      step: ->
        console.log @anim.toString() if @anim.ticks % 100 is 0
        @flock(a) for a in @agents
        @reportFlockVectorSize() if @anim.ticks % 10 is 0
        if @anim.draws is 2 # Show the sprite sheet if there is one after first draw
          sheet = u.last(u.s.spriteSheets) if u.s.spriteSheets.length isnt 0
          if sheet?
            console.log sheet
            document.getElementById("play").appendChild(sheet.canvas)
        # @stop() if @anim.ticks is 100

      flock: (a) -> # a is agent
        flockmates = @agents.inRadius a, @vision
        if flockmates.length isnt 0
          [nearestNeighbor, d] = u.minOneOf flockmates, ((f) -> f.distance a), true
          if d < @minSeparation
            @separate a, nearestNeighbor
          else
            @align a, flockmates
            @cohere a, flockmates
        a.forward .5 # move partial patch for smooth animation.

      separate: (a, nearest) ->
        h = nearest.towards a; @turnTowards a, h
      align: (a, flockmates) ->
        @turnTowards a, @averageHeading(flockmates)
      cohere: (a, flockmates) ->
        @turnTowards a, @averageHeadingTowards(a, flockmates)
        
      turnTowards: (a, heading) ->
        turn = u.subtractRads heading, a.heading # angle from h to a
        turn = u.clamp turn, -@maxTurn, @maxTurn # limit the turn
        a.rotate turn
      averageHeading: (flockmates) ->
        dx = (Math.sin f.heading for f in flockmates).reduce (x,y) -> x+y
        dy = (Math.cos f.heading for f in flockmates).reduce (x,y) -> x+y
        @safeAtan dy, dx
      averageHeadingTowards: (a, flockmates) ->
        dx = (Math.sin f.towards a for f in flockmates).reduce (x,y) -> x+y
        dy = (Math.cos f.towards a for f in flockmates).reduce (x,y) -> x+y
        @safeAtan dy, dx
      safeAtan: (x,y) -> if x is 0 and y is 0 then 0 else Math.atan y/x # atan ok if x 0
      
      reportFlockVectorSize: ->
        dx = (Math.sin a.heading for a in @agents).reduce (x,y) -> x+y
        dy = (Math.cos a.heading for a in @agents).reduce (x,y) -> x+y
        alignment = Math.sqrt(dx*dx + dy*dy)/@population

    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, hasNeighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new MyModel "layers", 15, -16, 16, -16, 16, true, false
    # APP=new MyModel "layers", 16, 0, 78, 0, 40, true, false
    APP.setRootVars() # Put Model vars in global name space -- only for demo & debug

    </script>
  </head>
  <body onload="ABM.model.start()">
    <canvas id="test" style="height:0;width:0">Browser doesn't support HTML5 Canvas</canvas>
    <div id="layers" style="position:relative;padding:20;"></div>
    <div id="play" style="float:right;"></div>
  </body>
</html>