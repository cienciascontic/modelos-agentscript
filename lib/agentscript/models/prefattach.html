<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: -> # called by Model ctor
        @refreshPatches = false # for static patches
        
        # globals
        @circleLayout = true
        @hist = []
        @stopTick = 400
        
        # defaults
        @agents.setDefaultShape "circle"
        @links.setDefaultThickness .75 # Easier to see thiner link
        
        @agents.create 1; @makeNode @agents[0]
      step: ->
        @makeNode @findPartner()
        a.size = Math.sqrt a.linkNeighbors().length for a in @agents
        @links.layoutCircle @agents, @patches.maxX * 0.9
        @hist = u.histOf @agents, 1, (a) -> a.linkNeighbors().length
        console.log "Tick: #{@anim.ticks} Histogram: [#{@hist}]" if @anim.ticks % 25 is 0
        (console.log "Stopping at stopTick: #{@anim.ticks}"; @stop()) if @anim.ticks is @stopTick

      makeNode: (toNode) ->
        @agents.create 1, (a) =>
          @links.create a, toNode
          a.moveTo toNode; a.forward 8
      findPartner: ->
        u.oneOf @links.oneOf().bothEnds() # or @links.allEnds().oneOf()

    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, hasNeighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new MyModel "layers", 5, -45, 45, -45, 45, false, false
    APP.setRootVars() # Put Model vars in global name space -- only for demo & debug

    </script>
  </head>
  <body onload="ABM.model.start()">
    <canvas id="test" style="height:0;width:0">Browser doesn't support HTML5 Canvas</canvas>
    <div id="layers" style="position:relative;padding:20;"></div>
  </body>
</html>