<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: -> # called by Model ctor
        @refreshPatches = false # for static patches
        @refreshLinks = false # for static links .. drown as created
        
        # globals
        @buttons = 200
        @cluster = 0
        
        # defaults
        @agents.setDefaultShape "circle"
        @agents.setDefaultSize 1.5
        @agents.setDefaultHeading 0 # override promotion to random angle
        @links.setDefaultThickness .5 # 2.5
        
        
        @agents.create @buttons, (a) => # fat arrow for @patches etc
          # a.size = 1.5
          # a.shape = "circle"
          a.setXY @patches.randomPt()...

      step: ->
        b1 = @agents.oneOf()
        b2 = @agents.other(b1).oneOf()
        
        @links.create b1, b2, (l) ->
          l.draw ABM.contexts.links
        
        g = @graphOf b1
        c = u.minOneOf(g, "id").color
        node.color = c for node in g
        if g.length > @cluster
          console.log "New largest cluster: size: #{g.length} tick: #{@anim.ticks}"
        @cluster = Math.max @cluster, g.length
        
        if @cluster is @buttons
          console.log "Completed at tick #{@anim.ticks}:  Cluster size #{@cluster} Links: #{@links.length}"
          @stop()
        
      graphOf: (node) ->
        a.marked = false for a in @agents
        @markNeighbors node
        a for a in @agents when a.marked
      markNeighbors: (node) ->
        node.marked = true
        unMarked = (n for n in node.linkNeighbors() when not n.marked)
        @markNeighbors n for n in unMarked


    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, hasNeighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new MyModel "layers", 13, -16, 16, -16, 16, false, false
    APP.setRootVars() # Put Model vars in global name space -- only for demo & debug

    </script>
  </head>
  <body onload="ABM.model.start()">
    <canvas id="test" style="height:0;width:0">Browser doesn't support HTML5 Canvas</canvas>
    <div id="layers" style="position:relative;padding:20;"></div>
  </body>
</html>